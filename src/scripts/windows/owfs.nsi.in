# $Id$
# Part of the OWFS (One Wire Filesystem)
# See http://owfs.sourceforge.net
# or http://www.owfs/org
# Copyright 2006 Paul H Alfille
#
# Windows installer script
# Using NSIS from Nullsoft
#
# file src/windows/owfs.nsi

#########
# Version
#########
!ifndef VERSION
  !define VERSION '@VERSION@'
!endif

#########
# directories -- up from src/windows/scripts
#########
!define ROOT_OWFS "..\..\.."
!define ROOT_MODULE "${ROOT_OWFS}\module"
!define ROOT_HOME "${ROOT_OWFS}\..\.."
!define ROOT_CYGWIN "${ROOT_HOME}\.."

#########
# Install settings
#########
SetCompressor /SOLID lzma
SetOverwrite ifnewer
CRCCheck on

#########
# Name
#########
Name "OWFS"
Caption "OWFS (One Wire Filesystem) ${VERSION} Setup"
BrandingText /TRIMCENTER "http://www.owfs.org"
Icon "owfs.ico"
UninstallIcon "unowfs.ico"

#########
# Location
#########
# The default installation directory
InstallDir $PROGRAMFILES\OWFS
# Result
!ifdef OUTFILE
  OutFile "${OUTFILE}"
!else
  OutFile ${ROOT_OWFS}\owfs_${VERSION}.exe
!endif


InstType "Programs"
InstType "Source"

#########
# License
#########
LicenseData "${ROOT_OWFS}\COPYING"

#########
# Pages
#########
Page license
Page components
Page directory
Page instfiles
UninstPage uninstConfirm
UninstPage instfiles

#########
# Files
#########
Section "OWFS program files" Bins

  SetDetailsPrint textonly
  DetailPrint "Installing OWFS Program Files..."
  SetDetailsPrint listonly

  SectionIn 1 2 RO
  SetOutPath $INSTDIR
  File "${ROOT_MODULE}\owshell\src\c\owdir.exe"
  File "${ROOT_MODULE}\owshell\src\c\owread.exe"
  File "${ROOT_MODULE}\owshell\src\c\owwrite.exe"
  File "${ROOT_MODULE}\owshell\src\c\owpresent.exe"
  File "${ROOT_MODULE}\owshell\src\c\owdir.exe"
  File "${ROOT_MODULE}\owhttpd\src\c\owhttpd.exe"
  File "${ROOT_MODULE}\owserver\src\c\owserver.exe"
  File "${ROOT_MODULE}\owftpd\src\c\owftpd.exe"
  File "${ROOT_CYGWIN}\bin\cygwin1.dll"
  File "*.ico"
  WriteUninstaller "$INSTDIR\owfs_uninstall.exe"
  WriteRegStr HKLM "Software\OWFS\Version" "String Value" "$Version"

SectionEnd

Section "OWFS source files" Source

  SetDetailsPrint textonly
  DetailPrint "Installing OWFS Source Files..."
  SetDetailsPrint listonly

  SectionIn 2
  SetOutPath $INSTDIR\source
  File /r "${ROOT_OWFS}\*.c"
  File /r "${ROOT_OWFS}\*.h"
  File /r "${ROOT_OWFS}\*.am"

  SetOutPath $INSTDIR\source\src\man
  File /r "${ROOT_OWFS}\src\man\*"

SectionEnd

Section "Add to Start menu"
  CreateDirectory "$SMPROGRAMS\OWFS"
  CreateShortcut "$SMPROGRAMS\OWFS\owserver.lnk" "$INSTDIR\owserver.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION Server/Multiplexor"
  CreateShortcut "$SMPROGRAMS\OWFS\owftpd.lnk" "$INSTDIR\owftpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION FTP server"
  CreateShortcut "$SMPROGRAMS\OWFS\owhttpd.lnk" "$INSTDIR\owhttpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION Web server"
  CreateShortcut "$SMPROGRAMS\OWFS\owfs_uninstall.lnk" "$INSTDIR\owfs_uninstall.exe" "" "$INSTDIR\unowfs.ico" "" "" "" "Uninstall OWFS $VERSION"
SectionEnd

Section "Add to Desktop"
  CreateShortcut "$DESKTOP\owserver.lnk" "$INSTDIR\owserver.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION Server/Multiplexor"
  CreateShortcut "$DESKTOP\owftpd.lnk" "$INSTDIR\owftpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION FTP server"
  CreateShortcut "$DESKTOP\owhttpd.lnk" "$INSTDIR\owhttpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS $VERSION Web server"
  CreateShortcut "$DESKTOP\owfs_uninstall.lnk" "$INSTDIR\owfs_uninstall.exe" "" "$INSTDIR\unowfs.ico" "" "" "" "Uninstall OWFS $VERSION"
SectionEnd

Section "USB Support Installation"
  MessageBox MB_OK "Going to LibUsb-Win32 site for installation."
  ExecShell "open" "http://libusb-win32.sourceforge.net/"
SectionEnd

Section "Cygwin Installation"
  MessageBox MB_OK "Going to RedHat's Cygwin site for installation."
  ExecShell "open" "http://www.redhat.com/services/custom/cygwin/"
SectionEnd

Section "Uninstall"
  RMDir /r /REBOOTOK $INSTDIR
  RMDir /r /REBOOTOK "$SMPROGRAMS\OWFS"
  Delete "$DESKTOP\owserver.lnk"
  Delete "$DESKTOP\owftpd.lnk"
  Delete "$DESKTOP\owhttpd.lnk"
  Delete "$DESKTOP\owfs_uninstall.lnk"
  DeleteRegKey HKLM "Software\OWFS\Version"
SectionEnd
