# $Id$
# Part of the OWFS (One Wire Filesystem)
# See http://owfs.sourceforge.net
# or http://www.owfs/org
# Copyright 2006 Paul H Alfille
#
# Windows installer script
# Using NSIS from Nullsoft
#
# file src/windows/owfs.nsi

#########
# Interface
#########
!include "MUI.nsh"
# Use Logic Library to improve readability
!include "LogicLib.nsh"

#########
# Version
#########
!ifndef VERSION
  !define VERSION '@Version@'
!endif

#########
# directories -- up from src/windows/scripts
#########
!define ROOT_OWFS "..\..\.."
!define OPTDIR "\cygwin\opt\owfs"
!define ROOT_HOME "${ROOT_OWFS}\..\.."
!define ROOT_CYGWIN "${ROOT_HOME}\.."

#########
# Install settings
#########
SetCompressor /SOLID lzma
SetOverwrite ifnewer
CRCCheck on

#########
# Name
#########
Name "OWFS"
Caption "OWFS (One Wire Filesystem) ${VERSION} Setup"
BrandingText "One-Wire Filesystem http://www.owfs.org"
Icon "owfs.ico"
UninstallIcon "unowfs.ico"

#########
# Location
#########
# The default installation directory
InstallDir $PROGRAMFILES\OWFS
# Result
!ifdef OUTFILE
  OutFile "${OUTFILE}"
!else
  OutFile ${ROOT_OWFS}\owfs_${VERSION}.exe
!endif


InstType "Programs"
InstType "Source"

#########
# Pages
#########
 !insertmacro MUI_PAGE_WELCOME
 !insertmacro MUI_PAGE_LICENSE "${ROOT_OWFS}\COPYING"
 !insertmacro MUI_PAGE_COMPONENTS
 !insertmacro MUI_PAGE_DIRECTORY
 !insertmacro MUI_PAGE_INSTFILES
 !insertmacro MUI_PAGE_FINISH

 !insertmacro MUI_UNPAGE_WELCOME
 !insertmacro MUI_UNPAGE_CONFIRM
 !insertmacro MUI_UNPAGE_INSTFILES
 !insertmacro MUI_UNPAGE_FINISH

 !define      MUI_ABORTWARNING

 !insertmacro MUI_LANGUAGE "English"

#########
# Kill running programs
#########
Section "Terminate currently running OWFS programs"
  ExecWait '"tskill" "owdir"'
  ExecWait '"tskill" "owread"'
  ExecWait '"tskill" "owwrite"'
  ExecWait '"tskill" "owpresent"'
  ExecWait '"tskill" "owserver"'
  ExecWait '"tskill" "owhttpd"'
  ExecWait '"tskill" "owftpd"'
SectionEnd

#########
# Files
#########
Section "OWFS program files" Bins

  SetDetailsPrint textonly
  DetailPrint "Installing OWFS Program Files..."
  SetDetailsPrint listonly

  SectionIn 1 2 RO
  SetOutPath $INSTDIR
  File /r "${OPTDIR}\*"
  File /oname=bin\libusb0.dll ${OPTDIR}\bin\cygusb0.dll
  File /oname=drivers\libusb0.sys "\cygwin\lib\libusb\libusb0.sys"
  File "*.ico"
  WriteUninstaller "$INSTDIR\owfs_uninstall.exe"
  WriteRegStr HKLM "Software\OWFS\Version" "String Value" "${VERSION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OWFS" "DisplayName" "OWFS -- One Wire Filesystem"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OWFS" "UninstallString" '"$INSTDIR\owfs_uninstall.exe"'

SectionEnd

#########
# USB driver
#########
Section "Install USB Driver" SecUsbDrv
  SectionIn 1
#  Call IsNt
#  Pop $R0
#  ${If} $R0 != false
    SetOutPath "$WINDIR\system32"
    File /oname=libusb0.dll ${OPTDIR}\bin\cygusb0.dll
#    ExecWait 'rundll32 libusb0.dll,usb_install_driver_np_rundll $INSTDIR\driver\apcupsd.inf'
    ExecWait 'rundll32 libusb0.dll,usb_install_driver_np_rundll $INSTDIR\driver\ds9490.inf'
#  ${Else}
#    MessageBox MB_OK "The USB driver cannot be automatically installed on Win98 or WinMe. \
#                      Please see $INSTDIR\driver\install.txt for instructions on installing \
#                      the driver by hand."
#  ${EndIf}
SectionEnd

#########
# Start Menu
#########
Section "Add to Start menu"
  SectionIn 1
  CreateDirectory "$SMPROGRAMS\OWFS"
  Delete "$SMPROGRAMS\OWFS\owserver.lnk"
  Delete "$SMPROGRAMS\OWFS\owftpd.lnk"
  Delete "$SMPROGRAMS\OWFS\owhttpd.lnk"
  Delete "$SMPROGRAMS\OWFS\owfs_uninstall.lnk"
  CreateShortcut "$SMPROGRAMS\OWFS\owserver.lnk" "$INSTDIR\bin\owserver.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} Server/Multiplexor"
  CreateShortcut "$SMPROGRAMS\OWFS\owftpd.lnk" "$INSTDIR\bin\owftpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} FTP server"
  CreateShortcut "$SMPROGRAMS\OWFS\owhttpd.lnk" "$INSTDIR\bin\owhttpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} Web server"
  CreateShortcut "$SMPROGRAMS\OWFS\owfs_uninstall.lnk" "$INSTDIR\owfs_uninstall.exe" "" "$INSTDIR\unowfs.ico" "" "" "" "Uninstall OWFS ${VERSION}"
SectionEnd

#########
# Desktop
#########
Section "Add to Desktop"
  SectionIn 1
  Delete "$DESKTOP\owserver.lnk"
  Delete "$DESKTOP\owftpd.lnk"
  Delete "$DESKTOP\owhttpd.lnk"
  Delete "$DESKTOP\owfs_uninstall.lnk"
  CreateShortcut "$DESKTOP\owserver.lnk" "$INSTDIR\bin\owserver.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} Server/Multiplexor"
  CreateShortcut "$DESKTOP\owftpd.lnk" "$INSTDIR\bin\owftpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} FTP server"
  CreateShortcut "$DESKTOP\owhttpd.lnk" "$INSTDIR\bin\owhttpd.exe" "" "$INSTDIR\owfs.ico" "" "" "" "OWFS ${VERSION} Web server"
  CreateShortcut "$DESKTOP\owfs_uninstall.lnk" "$INSTDIR\owfs_uninstall.exe" "" "$INSTDIR\unowfs.ico" "" "" "" "Uninstall OWFS ${VERSION}"
SectionEnd

#########
# Source
#########
Section "OWFS source files" Source

  SetDetailsPrint textonly
  DetailPrint "Installing OWFS Source Files..."
  SetDetailsPrint listonly

  SectionIn 2
  SetOutPath $INSTDIR\source
  File /r "${ROOT_OWFS}\*.c"
  File /r "${ROOT_OWFS}\*.h"
  File /r "${ROOT_OWFS}\*.am"

  SetOutPath $INSTDIR\source\src\man
  File /r "${ROOT_OWFS}\src\man\*"

SectionEnd

Section "USB Support Installation"
  MessageBox MB_OK "Going to LibUsb-Win32 site for installation."
  ExecShell "open" "http://libusb-win32.sourceforge.net/"
SectionEnd

Section "Cygwin Installation"
  MessageBox MB_OK "Going to RedHat's Cygwin site for installation."
  ExecShell "open" "http://www.cygwin.com"
SectionEnd

Section "Uninstall"
  RMDir /r /REBOOTOK $INSTDIR
  RMDir /r /REBOOTOK "$SMPROGRAMS\OWFS"
  Delete "$DESKTOP\owserver.lnk"
  Delete "$DESKTOP\owftpd.lnk"
  Delete "$DESKTOP\owhttpd.lnk"
  Delete "$DESKTOP\owfs_uninstall.lnk"
  DeleteRegKey HKLM "Software\OWFS\Version"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OWFS"

SectionEnd
